buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
    }
}

plugins  {
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getSonatypeRepositoryToken() { return hasProperty('ossrhToken') ? ossrhToken : "" }

def getSonatypeRepositoryTokenPassword() { return hasProperty('ossrhTokenPassword') ? ossrhTokenPassword : "" }

nexusPublishing {
    packageGroup = GROUP //optional if packageGroup == project.getGroup()
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = getSonatypeRepositoryToken()
            password = getSonatypeRepositoryTokenPassword()
        }
    }
    transitionCheckOptions {
        maxRetries.set(240)
        delayBetween.set(Duration.ofMillis(30000))
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "io.github.gradle-nexus.publish-plugin"
